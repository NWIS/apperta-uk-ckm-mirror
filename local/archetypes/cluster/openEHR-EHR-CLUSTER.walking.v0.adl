archetype (adl_version=1.4; uid=34732d93-9c22-4d7b-a76f-8ce17bf1dc92)
	openEHR-EHR-CLUSTER.walking.v0

concept
	[at0000]	-- Walking
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heidi Koikkalainen">
		["organisation"] = <"freshEHR Clinical Informatics Ltd.">
		["email"] = <"heidi@freshehr.com">
		["date"] = <"2020-10-15">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the practical ability of an individual to walk, including their identified support needs.">
			use = <"Use to record details about the practical ability of an individual to walk, including their identified support needs.">
			keywords = <"walk, walking, gait, distance", ...>
			misuse = <"">
			copyright = <"© Apperta Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"Apperta UK">
		["original_namespace"] = <"uk.org.clinicalmodels">
		["original_publisher"] = <"Apperta UK">
		["custodian_namespace"] = <"uk.org.clinicalmodels">
		["MD5-CAM-1.0.1"] = <"10518DF166FED2369D9C59CF8A5626A7">
		["build_uid"] = <"4586ab08-40d6-4eae-afb2-9cfe984add08">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Walking
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Type
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0003,    -- Indoors
							at0004,    -- Outdoors
							at0005]    -- Other
						}
					}
				}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {    -- Ability
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0007,    -- Independent without equipment/aids
							at0008,    -- Independent with equipment/aids
							at0009,    -- Requires support
							at0010,    -- Unable to walk
							at0011]    -- Not known
						}
					}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {    -- Support required
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0013,    -- Verbal prompt
							at0014,    -- Physical prompt
							at0015,    -- Support from one person
							at0016]    -- Support from two or more people
						}
					}
				}
			}
			CLUSTER[at0017] occurrences matches {0..*} matches {    -- Equipment/aids
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0018] occurrences matches {0..1} matches {    -- Equipment description
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0019] occurrences matches {0..1} matches {    -- Equipment details
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
			ELEMENT[at0022] occurrences matches {0..1} matches {    -- Gait
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0023] occurrences matches {0..1} matches {    -- Distance
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0020] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {    -- Extension
				include
					archetype_id/value matches {/.*/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Walking">
					description = <"The ability of an individual to walk.">
				>
				["at0001"] = <
					text = <"Description">
					description = <"Narrative description of the individual's overall ability to walk.">
				>
				["at0002"] = <
					text = <"Type">
					description = <"Description of the type of walking activity, for example, walking indoors or outdoors.">
				>
				["at0003"] = <
					text = <"Indoors">
					description = <"Walking indoors.">
				>
				["at0004"] = <
					text = <"Outdoors">
					description = <"Walking outdoors.">
				>
				["at0005"] = <
					text = <"Other">
					description = <"Other type of walking activity.">
				>
				["at0006"] = <
					text = <"Ability">
					description = <"Description of the individual's level of ability to walk.">
				>
				["at0007"] = <
					text = <"Independent without equipment/aids">
					description = <"The individual is able to walk independently without any equipment/aids.">
				>
				["at0008"] = <
					text = <"Independent with equipment/aids">
					description = <"The individual is able to walk independently with equipment/aids.">
				>
				["at0009"] = <
					text = <"Requires support">
					description = <"The individual requires support from others to walk.">
				>
				["at0010"] = <
					text = <"Unable to walk">
					description = <"The individual is unable to walk.">
				>
				["at0011"] = <
					text = <"Not known">
					description = <"It is not known whether the individual is able to walk independently or requires support from others..">
				>
				["at0012"] = <
					text = <"Support required">
					description = <"Description of the type of support the individual requires to walk.">
				>
				["at0013"] = <
					text = <"Verbal prompt">
					description = <"The individual requires verbal prompting to walk.">
				>
				["at0014"] = <
					text = <"Physical prompt">
					description = <"The individual requires physical prompting to walk.">
				>
				["at0015"] = <
					text = <"Support from one person">
					description = <"The individual requires support from one person to walk.">
				>
				["at0016"] = <
					text = <"Support from two or more people">
					description = <"The individual requires support from two or more people to walk.">
				>
				["at0017"] = <
					text = <"Equipment/aids">
					description = <"Details of the walking equipment/aids that the individual requires to walk.">
				>
				["at0018"] = <
					text = <"Equipment description">
					description = <"Narrative description of the equipment/aid.">
				>
				["at0019"] = <
					text = <"Equipment details">
					description = <"Structured details of the equipment/aid.">
				>
				["at0020"] = <
					text = <"Comment">
					description = <"Additional narrative about the individual's ability to walk not captured in other fields.">
				>
				["at0021"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
				>
				["at0022"] = <
					text = <"Gait">
					description = <"Description of the individual's manner of walking.">
				>
				["at0023"] = <
					text = <"Distance">
					description = <"Description of the distance of a walking activity.">
				>
			>
		>
	>
