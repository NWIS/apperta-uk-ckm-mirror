archetype (adl_version=1.4; uid=20ddf82e-f326-41fa-bd33-a39837c2bf87)
	openEHR-EHR-CLUSTER.oral_hygiene.v0

concept
	[at0000]	-- Oral hygiene
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heidi Koikkalainen">
		["organisation"] = <"freshEHR Clinical Informatics Ltd.">
		["email"] = <"heidi@freshehr.com">
		["date"] = <"2020-10-09">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the practical ability of an individual to carry out oral hygiene activities, such as brushing teeth or cleaning dentures, and their identified support needs.">
			use = <"Use to record details about the practical ability of an individual to carry out oral hygiene activities, such as brushing teeth or cleaning dentures, and their identified support needs.">
			keywords = <"brushing, teeth, denture, mouthwash", ...>
			misuse = <"">
			copyright = <"© Apperta Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"Apperta UK">
		["original_namespace"] = <"uk.org.clinicalmodels">
		["original_publisher"] = <"Apperta UK">
		["custodian_namespace"] = <"uk.org.clinicalmodels">
		["MD5-CAM-1.0.1"] = <"71296CE36B0E47C4B676A7043A1F31D4">
		["build_uid"] = <"4e4f3da9-1700-4cd1-a1b0-1b71fc68307f">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Oral hygiene
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Denture
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0003,    -- Upper
							at0004,    -- Lower
							at0005]    -- Partial
						}
					}
				}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {    -- Ability
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0007,    -- Independent without equipment/aids
							at0008,    -- Independent with equipment/aids
							at0009,    -- Requires support
							at0010,    -- Unable to manage personal care
							at0011]    -- Not known
						}
					}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {    -- Support required
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0013,    -- Verbal prompt
							at0014,    -- Physical prompt
							at0015,    -- Support from one person
							at0016]    -- Support from two or more people
						}
					}
				}
			}
			CLUSTER[at0017] occurrences matches {0..*} matches {    -- Equipment/aids
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0018] occurrences matches {0..1} matches {    -- Equipment description
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0019] occurrences matches {0..1} matches {    -- Equipment details
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
			ELEMENT[at0020] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {    -- Extension
				include
					archetype_id/value matches {/.*/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Oral hygiene">
					description = <"The ability of an individual to carry out oral hygiene activities, such as brushing teeth or cleaning dentures.">
				>
				["at0001"] = <
					text = <"Description">
					description = <"Narrative description of the individual's overall ability to carry out oral hygiene activities.">
				>
				["at0002"] = <
					text = <"Denture">
					description = <"Indicates whether the individual has any dentures.">
				>
				["at0003"] = <
					text = <"Upper">
					description = <"Upper denture.">
				>
				["at0004"] = <
					text = <"Lower">
					description = <"Lower denture.">
				>
				["at0005"] = <
					text = <"Partial">
					description = <"Partial denture.">
				>
				["at0006"] = <
					text = <"Ability">
					description = <"Description of the individual's level of ability to carry out oral hygiene activities.">
				>
				["at0007"] = <
					text = <"Independent without equipment/aids">
					description = <"The individual is able to take care of their oral hygiene independently without any equipment/aids.">
				>
				["at0008"] = <
					text = <"Independent with equipment/aids">
					description = <"The individual is able to take care of their oral hygiene independently with equipment/aids.">
				>
				["at0009"] = <
					text = <"Requires support">
					description = <"The individual requires support from others to take of their oral hygiene.">
				>
				["at0010"] = <
					text = <"Unable to manage personal care">
					description = <"The individual is unable to take care of their oral hygiene and is totally dependent on others.">
				>
				["at0011"] = <
					text = <"Not known">
					description = <"It is not known whether the individual is independent or requires support to take care of their oral hygiene.">
				>
				["at0012"] = <
					text = <"Support required">
					description = <"Description of the type of support the individual requires to carry out oral hygiene activities.">
				>
				["at0013"] = <
					text = <"Verbal prompt">
					description = <"The individual requires verbal prompting to carry out oral hygiene activities.">
				>
				["at0014"] = <
					text = <"Physical prompt">
					description = <"The individual requires physical prompting to carry out oral hygiene activities.">
				>
				["at0015"] = <
					text = <"Support from one person">
					description = <"The individual requires support from one person to carry out oral hygiene activities.">
				>
				["at0016"] = <
					text = <"Support from two or more people">
					description = <"The individual requires support from two or more people to carry out oral hygiene activities.">
				>
				["at0017"] = <
					text = <"Equipment/aids">
					description = <"Details of the equipment/aids that the individual requires to carry out oral hygiene activities.">
				>
				["at0018"] = <
					text = <"Equipment description">
					description = <"Narrative description of the equipment/aid.">
				>
				["at0019"] = <
					text = <"Equipment details">
					description = <"Structured details of the equipment/aid.">
				>
				["at0020"] = <
					text = <"Comment">
					description = <"Additional narrative about the individual's ability to carry out oral hygiene activities not captured in other fields.">
				>
				["at0021"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
				>
			>
		>
	>
